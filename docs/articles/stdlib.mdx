---
title: "The standard library"
slug: "/stdlib"
---

# The standard library

Basic types and functions provided by the runtime.

```
import std;
```

## Contents

- [Basic types and functions](#basic-types-and-functions)

  - [type Boolean](#type-boolean)
  - [type Integer](#type-integer)
  - [type Float](#type-float)
  - [type Null](#type-null)
  - [type String](#type-string)
  - [type Symbol](#type-symbol)
  - [type Function](#type-function)
  - [type Module](#type-module)
  - [type NativeObject](#type-nativeobject)
  - [type NativePointer](#type-nativepointer)
  - [type Type](#type-type)
  - [func type_of](#func-type_of)

- [Containers](#containers)

  - [type Tuple](#type-tuple)
  - [type Record](#type-record)
  - [type Array](#type-array)
  - [type Buffer](#type-buffer)
  - [type Map](#type-map)
  - [type Set](#type-set)

- [Strings](#strings)

  - [type StringSlice](#type-stringslice)
  - [type StringBuilder](#type-stringbuilder)
  - [func new_string_builder](#func-new_string_builder)

- [Input and Output](#input-and-output)

  - [func print](#func-print)
  - [func loop_timestamp](#func-loop_timestamp)

- [Coroutines](#coroutines)

  - [type Coroutine](#type-coroutine)
  - [type CoroutineToken](#type-coroutinetoken)
  - [func current_coroutine](#func-current_coroutine)
  - [func launch](#func-launch)
  - [func dispatch](#func-dispatch)
  - [func coroutine_token](#func-coroutine_token)
  - [func yield_coroutine](#func-yield_coroutine)

- [Error handling](#error-handling)

  - [type Result](#type-result)
  - [func success](#func-success)
  - [func failure](#func-failure)
  - [type Exception](#type-exception)
  - [func panic](#func-panic)
  - [func catch_panic](#func-catch_panic)

## Basic types and functions

### type Boolean

The type of `true` and `false`.

### type Integer

The type of integer expressions such as `7` or `0x1F`.

### type Float

Type type of floating point expressions such as `1.25`.

### type Null

The type of the constant `null`.

### type String

The type of all string expressions.
Strings are immutable.

TODO: Unicode strings

TODO: Internal representation is guaranteed to be UTF-8

### type Symbol

The type of symbol literals, e.g. `#answer`.

### type Function

The type of all functions.

### type Module

The type of all modules:

```
import std;
// std.type_of(std) == std.Module
```

### type NativeObject

The type of native objects that have been exposed to tiro.
The tiro runtime manages the lifetime (and the storage) of these objects, which usually includes invoking a native finalizer when the object is collected.

### type NativePointer

The type of a native pointer that has been exposed to tiro.
The tiro runtime does _not_ manage these objects in any way, which makes them very cheap to use.

### type Type

The type of all types. In other words, `std.type_of(value)` will always return an instance of this type for any kind of value.

### func type_of

```
func type_of(v: Value): Type;
```

Returns the type of `v`. For example:

```
assert(std.type_of(true) == std.Boolean);
```

## Containers

### type Tuple

The type of all tuples, e.g. `()`, `(1,)` or `(1, 2)`.

### type Record

The type of all records, e.g. `(:)` or `(foo: "bar")`.

### type Array

The type of all arrays, e.g. `[]` and `[a, b, c]`.

An array is a dynamic sequence of items that can grow or shrink as needed.
Existing items can be read or written using the indexing operator, e.g. `const first = array[0]` or `array[0] = "first"`.
The sequence of items can be manipulated using the methods below.

#### func Array.size

```
func Array.size(): Integer;
```

Returns the number of items in the array.

#### func Array.append

```
func Array.append(v: Value);
```

Appends `v` to the array.

#### func Array.clear

```
func Array.clear()
```

Removes all items from the array.

### type Buffer

Buffers reference a raw region of memory, i.e. a blog of bytes.
They are especially useful when doing I/O or when manipulating large amounts of data.

TODO: usage

#### func Buffer.size

```
func Buffer.size(): Integer;
```

Returns the number of items in the buffer.

### type Map

A map is a container that associates _keys_ with _values_.
The key-value associations can be manipulated at will.
Given a key, a map allows for efficient lookup (and update) of the associated value.

Maps are implemented as [hash tables](https://en.wikipedia.org/wiki/Hash_table).

#### Insertion order {#map-insertion-order}

Contrary to most other hash table implementations, tiro's map preserves the insertion order of its mappings.
When iterating a map, keys and values are visited in the order in which they were inserted:

- A new key-value mapping (i.e. a key that was not already in the map) is always inserted at the end.
- Updating an existing key-value mapping (i.e. key already exists in the map) does not change its order in the map.
- Removing a key-value mapping preserves the order of all remaining mappings.

<span className="caption">Example: iterating over a map</span>

```
import std;

export func main() {
    const m = map{};
    m[1] = "one";
    m[2] = "two";
    m[3] = "three";

    // Guaranteed output:
    // 1 one
    // 2 two
    // 3 three
    for (key, value) in m {
        std.print(key, value);
    }
}
```

#### func Map.size

```
func Map.size(): Integer;
```

Returns the number of key-value mappings in the map.

#### func Map.contains

```
func Map.contains(key: Value): Boolean;
```

Returns `true` if there is a value associated with `key`, `false` otherwise.

#### func Map.keys

```
func Map.keys(): Iterable;
```

Returns an iterable view over the keys within the map.
The iteration order is the same as the map's order.

#### func Map.values

```
func Map.values(): Iterable;
```

Returns an iterable view over the values within the map.
The iteration order is the same as the map's order.

#### func Map.clear

```
func Map.clear();
```

Removes all key-value mappings from the map.

#### func Map.remove

```
func Map.remove(key: Value);
```

Removes the key-value mapping for `key`.
Does nothing if there is no such mapping.

TODO: Return true/false?

### type Set

A set is a container that stores unique items.
Items can be inserted or removed at will, and their existence can be tested efficiently.

Like maps, they are implemented as [hash tables](https://en.wikipedia.org/wiki/Hash_table).

#### Insertion order {#set-insertion-order}

When iterating a set, its items are visited in the order they were inserted in.
See also [map insertion order](#map-insertion-order).

#### func Set.size

```
func Set.size(): Integer;
```

Returns the number of items in the set.

#### func Set.contains

```
func Set.contains(item: Value): Boolean;
```

Returns `true` if the set contains `item`, `false` otherwise.

#### func Set.clear

```
func Set.clear();
```

Removes all items from the set.

#### func Set.insert

```
func Set.insert(item: Value): Boolean;
```

Inserts `item` into the set.
Returns `true` if item has been inserted, `false` if it already existed.

#### func Set.remove

```
func Set.remove(item: Value);
```

Removes `item` from the set.
Does nothing if `item` does not exist in the set.

TODO: Return true/false?

## Strings

### type StringSlice

### type StringBuilder

### func new_string_builder

## Input and Output

### func print

```
func print(values...: Value...);
```

Prints the given values followed by a new line.

### func loop_timestamp

```
func loop_timestamp(): Integer;
```

Returns the current loop timestamp.
The loop timestamp is refreshed on every iteration of the runtime's main loop.
A single iteration involves calling all ready coroutines and continues until all coroutines are either done or waiting for the completion of an asynchronous function.

Within the same iteration, `loop_timestamp()` will always return the same value.

## Coroutines

### type Coroutine

Coroutines represents threads of execution within a single operating system thread.
Coroutines are created through the native API or by calling `std.launch()`.

### type CoroutineToken

### func current_coroutine

```
func current_coroutine(): Coroutine;
```

Returns the current [Coroutine](#type-coroutine) instance, i.e. the coroutine from which `current_coroutine()` was called.

### func launch

```
func launch(fn: Function, args...: Value...): Coroutine;
```

Executes `fn(args...)` from within a new coroutine.
The coroutine instance is returned by this function.

TODO: Should we return a future instead?

### func dispatch

```
func dispatch();
```

Dispatches to other ready coroutines.
The current coroutine will resume execution after all other ready coroutines had the opportunity to run.

### func coroutine_token

```
func coroutine_token(): CoroutineToken;
```

Returns a [CoroutineToken](#type-coroutinetoken) instance suitable for resuming the current coroutine.
Coroutine tokens are invalidated after the coroutine has been resumed again.
See [yield_coroutine()](#func-yield_coroutine) for an example.

WARNING: This is a low level API designed to control the execution of a coroutine manually.
It should only be used to create new asynchronous abstractions.

### func yield_coroutine

```
func yield_coroutine();
```

Immediately pauses execution of the current coroutine.
Execution will only continue once the coroutine has been resumed via a [CoroutineToken](#type-coroutinetoken) previously obtained from [coroutine_token()](#func-coroutine_token).

WARNING: This is a low level API designed to control the execution of a coroutine manually.
It should only be used to create new asynchronous abstractions.

<span className="caption">Example: a simple Mutex</span>

```
import std;

export func main() {
    const mutex = createMutex();
    const task = func(id) {
        for var i = 0; i < 3; i += 1 {
            mutex.lock();
            std.print("Hello from ${id}");
            std.dispatch(); // Other coroutine can observe a locked mutex
            mutex.unlock();
        }
    };

    std.launch(task, "1");
    std.launch(task, "2");
}

// Creates a simplistic mutex that only supports two coroutines.
func createMutex() {
    // Coroutine token if another coroutine is already waiting.
    var waiter = null;
    var locked = false;

    return (
        lock: func() {
            if locked {
                if waiter != null {
                    std.panic("Cannot support more than one waiter");
                }
                // Wait until the other coroutine unlocks.
                waiter = std.coroutine_token();
                std.yield_coroutine();
            }
            // At this point, either the mutex wasn't initially locked
            // or yield_coroutine() returned, i.e. the other coroutine unlocked.
            assert(!locked);
            locked = true;
        },
        unlock: func() {
            if !locked {
                std.panic("Mutex was not locked");
            }
            locked = false;
            if waiter {
                waiter.resume();
                waiter = null;
            }
            // Give other coroutines the opportunity to run
            std.dispatch();
        }
    );
}
```

## Error handling

### type Result

A result represents the result of an operation that may fail.
The result type is the basic primitive type used in tiro's error handling facilities.

Results may be in one of two states:

- _Success_. Calling `result.value()` returns the result of the operation.

- _Failure_. Calling `result.reason()` returns a value explaining why the operation failed.

Results are constructed by calling the [success()](#func-success) and [failure()](#func-failure) functions.

<span className="caption">Example: simple error handling</span>

```
import std;

export func main() {
    const result1 = operation(5);
    std.print(result1.type(), result1.value()); // #success 10

    const result2 = operation(-5);
    std.print(result2.type(), result2.reason()); // #failure value too small!
}

func operation(n) {
     if (n < 0) {
        return std.failure("value too small!");
    } else {
        return std.success(n * 2);
    }
}
```

#### func Result.type

```
func Result.type(): Symbol;
```

Returns either `#success` or `#failure`, depending on the result's state.

#### func Result.is_success

```
func Result.is_success(): Boolean;
```

Returns `true` if the result represents success, `false` otherwise.

#### func Result.is_failure

```
func Result.is_failure(): Boolean;
```

Returns `true` if the result represents failure, `false` otherwise.

#### func Result.value

```
func Result.value(): Value;
```

Returns the result's value.
Requires that the result represents success.

#### func Result.reason

```
func Result.reason(): Value;
```

Returns the result's reason.
Requires that the result represents failure.

### func success

```
func success(value: Value): Result;
```

Returns a new successful [Result](#type-result) that holds the given `value`.

### func failure

```
func failure(reason: value): Result;
```

Returns a new failure [Result](#type-result) that holds the given `reason`.

### type Exception

Exceptions represent unexpected (and often fatal) errors encountered during program execution.
They are generated when code panics, for example by calling [panic()](#func-panic).

It is often preferable to use the less intrusive [Result](#type-result) type instead.
Panicking should be reserved for signaling critical error conditions such as programming errors.

TODO: Other properties (stack trace, user data, secondary exceptions)

#### func Exception.message

```
func Exception.message(): String;
```

Returns the exception's error message.

### func panic

```
func panic(message: String);
```

Immediately aborts execution of the current function with an error explained by `message`.
From within the `panic` function, a new [Exception](#type-exception) is constructed with the given `message` and [stack unwinding](https://en.wikipedia.org/wiki/Call_stack#Unwinding) is started.
Unwinding can be stopped by using the [catch_panic](#func-catch_panic) function.

This function is designed to report _unexpected_ errors such as programming errors.
Use the [Result](#type-result) type instead if errors can be expected, e.g. to report missing files.

### func catch_panic

```
func catch_panic(fn: Function): Result;
```

Invokes `fn()` and stops propagation of any non-fatal panic thrown by `fn` or its callees.
After execution of `fn()` completes (either through panic or through normal return), a [Result](#type-result) is returned:

- If `fn` returned normally, its return value is wrapped into a success result and returned.
- If a panic was caught, a failure result is returned. The result's `reason` is set to the caught value.

<span className="caption">Example: catching a panic from a function</span>

```
import std;

export func main() {
    const result = std.catch_panic(may_fail);
    if (result.is_success()) {
        std.print("success:", result.value());
    } else {
        std.print("failure:", result.reason().message());
    }
}

func may_fail() {
    // Replace `false` with `true` to return normally
    if (false) {
        return 42;
    } else {
        std.panic("help!");
    }
}
```
